AC_INIT(Makefile.am)

AC_CANONICAL_HOST
AC_CANONICAL_TARGET

AM_INIT_AUTOMAKE(liblmbcam,0.1.0)

AC_ARG_WITH([qt],
	    AC_HELP_STRING([--with-qt],
	                   [build Qt-based gui programs (default=yes)]),
            [], [with_qt=yes])

AC_ARG_ENABLE([examples],
	      AC_HELP_STRING([--enable-examples],
			     [compile examples (default=yes)]),
	      [], [enable_examples=yes])

AC_ARG_ENABLE([matlab],
	      AC_HELP_STRING([--enable-matlab],
			     [compile matlab interface (default=no)]),
	      [], [enable_matlab=no])

AM_CONDITIONAL([BUILD_QTLIBS], [test "$with_qt" = yes])
AM_CONDITIONAL([BUILD_EXAMPLES], [test "$enable_examples" = yes])
AM_CONDITIONAL([BUILD_LMBCAM_MATLAB], [test "$enable_matlab" = yes])

AC_PROG_CC
AC_PROG_CXX
AC_LANG(C++)
AC_PROG_LIBTOOL
AM_PROG_LIBTOOL
AC_PROG_INSTALL
AC_CACHE_SAVE

PKG_PROG_PKG_CONFIG([0.17])

if test $with_qt = "yes"; then
	PKG_CHECK_MODULES([qt3], [qt-mt >= 3.3.0])
	AC_PATH_PROG([qt3_MOC], [moc-qt3], [no])
	if test $qt3_MOC = "no"; then
	   AC_PATH_PROG([qt3_MOC], [moc], [no])
	   if test $qt3_MOC = "no"; then
	      AC_MSG_ERROR( Could not find moc)
	   fi
	fi
else
	enable_examples=no
fi

if test $enable_matlab = "yes"; then
	AC_CHECK_HEADER( mex.h, [], AC_MSG_ERROR( matlab headers are missing))
fi

AC_CACHE_SAVE

# Check for libraw1394
PKG_CHECK_MODULES([libraw1394], [libraw1394])

# Check for libdc1394
PKG_CHECK_MODULES([libdc1394], [libdc1394], [], [AC_CHECK_LIBDC])

if test -n $libdc1394_LIBS; then libdc1394_LIBS=-ldc1394_control; fi

# Check for libtif
AC_CHECK_HEADER( tiffio.h, [], AC_MSG_ERROR( libtiff headers are missing))
AC_CHECK_LIB( tiff, TIFFOpen, [], AC_MSG_ERROR( libtiff is missing))

# set the libtool shared library version numbers
lt_major=1
lt_revision=0
lt_age=0

AC_SUBST(lt_major)
AC_SUBST(lt_revision)
AC_SUBST(lt_age)

AC_CHECK_PROG(LIBCMDLINE_BIN,libcmdline-config,libcmdline-config)
if test -z "$LIBCMDLINE_BIN" ; then
   	  AC_MSG_ERROR(You need libcmdline-config. Perhaps check PATH)
fi	

AC_CHECK_PROG(LIBFIDWID_BIN,libfidwid-config,libfidwid-config)
if test -z "$LIBFIDWID_BIN" ; then
   	  AC_MSG_ERROR(You need libfidwid-config. Perhaps check PATH)
fi	

LIBCMDLINE_CFLAGS=`libcmdline-config --cflags`
LIBCMDLINE_LIBS=`libcmdline-config --libs`
LIBFIDWID_CFLAGS=`libfidwid-config --cflags`
LIBFIDWID_LIBS=`libfidwid-config --libs`

AC_SUBST(LIBCMDLINE_CFLAGS)
AC_SUBST(LIBCMDLINE_LIBS)
AC_SUBST(LIBFIDWID_CFLAGS)
AC_SUBST(LIBFIDWID_LIBS)

AC_CONFIG_FILES([Makefile])
AC_CONFIG_FILES([src/Makefile])
AC_CONFIG_FILES([src/liblmbcam/Makefile])
AC_CONFIG_FILES([src/liblmbcam/etc/Makefile])
AC_CONFIG_FILES([src/liblmbcam/scripts/Makefile])

AC_CONFIG_FILES([src/libqlmbcamwid/Makefile])
AC_CONFIG_FILES([src/libqlmbcam/Makefile])
AC_CONFIG_FILES([src/libqlmbcam/scripts/Makefile])
AC_CONFIG_FILES([src/lmbcam_matlab/Makefile])
AC_CONFIG_FILES([src/examples/Makefile])
AC_CONFIG_FILES([src/examples/multiview/Makefile])
AC_CONFIG_FILES([src/examples/qlmbcam/Makefile])

AC_OUTPUT

AC_MSG_RESULT([
-------------------------------------------------
Summary of liblmbcam build options:

build Qt dependent libraries          $with_qt
build examples                        $enable_examples
build matlab interface                $enable_matlab
-------------------------------------------------])
